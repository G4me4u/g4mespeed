buildscript {
    repositories {
		mavenCentral()
    }
    
    dependencies {
		classpath "com.guardsquare:proguard-gradle:7.2.0-beta2"
    }
}

plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
}

repositories {
    flatDir {
        dirs "./compat-mods"
    }
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/'
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "-mc" + project.minecraft_version
group = project.maven_group

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // ModMenu integration
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// Test mod compatibility in dev environment.
	//modImplementation ":sodium-fabric:mc1.16.1:0.1.0"
	//modImplementation ":optifabric:1.9.2"
	
	// Fabric API. This is technically optional, but you probably want it anyway.
    //modImplementation "net.fabricmc.fabric-api:fabric-api:0.31.0+1.16"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

def outputJarName = jar.archiveName.with { it.take(it.lastIndexOf(".")) }
if (outputJarName.endsWith("-dev")) {
	// Newest loom version also has the -dev suffix.
	outputJarName = outputJarName.with { it.take(it.lastIndexOf("-dev")) }
}
def obfuscatedFileName = "${outputJarName}-release.jar"
def homeDir = System.getProperty("user.home")

task obfuscateJar(type: proguard.gradle.ProGuardTask) {
	configuration "proguard.txt"

	injars "./build/libs/${outputJarName}.jar"
	outjars "./build/libs/${obfuscatedFileName}"
	
	// Automatically handle the Java version of this build.
	if (System.getProperty('java.version').startsWith('1.')) {
		// Before Java 9, the runtime classes were packaged in a single jar file.
		libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
	} else {
		// As of Java 9, the runtime classes are packaged in modular jmod files.
		libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
		libraryjars "${System.getProperty('java.home')}/jmods/java.sql.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
		//libraryjars "${System.getProperty('java.home')}/jmods/....."
	}
	
	obfuscationdictionary "member_obfuscation_dictionary.txt"
	
	printmapping "g4mespeed-mappings-mc${project.minecraft_version}.map"
	applymapping "g4mespeed-mappings-mc${project.minecraft_version}.map"
}

jar {
	from "LICENSE"
}

